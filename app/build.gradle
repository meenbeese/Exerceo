plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.health.openworkout"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 14
        versionName "1.3.1"
        archivesBaseName = "openWorkout-$versionName"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("../openWorkout.keystore")
            def keystoreProperties = new Properties()
            try {
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            } catch (FileNotFoundException ignored) {
                keystoreProperties = null
            }

            if (keystoreProperties != null) {
                storeFile file(rootDir.getCanonicalPath() + '/' + keystoreProperties['releaseKeyStore'])
                keyAlias keystoreProperties['releaseKeyAlias']
                keyPassword keystoreProperties['releaseKeyPassword']
                storePassword keystoreProperties['releaseStorePassword']
            }
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles(
                getDefaultProguardFile('proguard-android-optimize.txt'),
                'proguard-rules.pro'
            )
        }
        debug {
            debuggable true
            applicationIdSuffix '.debug'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    namespace 'com.health.openworkout'

    lint {
        abortOnError false
        checkReleaseBuilds false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Google
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.navigation:navigation-fragment:2.3.1'
    implementation 'androidx.navigation:navigation-ui:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.1'

    // GSon
    implementation 'com.google.code.gson:gson:2.8.5'

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Room DB
    implementation 'androidx.room:room-runtime:2.2.5'

    // Test
    annotationProcessor 'androidx.room:room-compiler:2.2.5'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    // Misc
    implementation 'cat.ereza:customactivityoncrash:2.2.0'
}
